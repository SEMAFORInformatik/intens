%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%                              Examples                                 %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsubsection{Examples}
\label{sec:opexamples}
The following example shows a sample \OPERATOR{} configuration
to display a matplotlib diagram created by a python program
scplot.py:
\begin{center}
\includegraphics[width=0.8\textwidth]{matplotlib-plot}
\end{center}

The python program creates the plot in SVG format
and returns it as a base64 encoded
string:

\lstset{basicstyle=\normalsize\ttfamily}
\begin{lstlisting}{language=python}
  ...
  imgbuffer = io.BytesIO()
  fig.savefig(imgbuffer, format='svg')
  plt.close(fig)
  # Convert the SVG data to base64
  svgplot = base64.b64encode(imgbuffer.getvalue()).decode()
  encoding = 'data:image/svg+xml;charset=utf-8;base64,'
  result = {}
  result['time'] = t.tolist()
  result['current'] = current.tolist()
  result['plot'] = encoding + svgplot
  json.dump(result, sys.stdout)
\end{lstlisting}

The plot is displayed in multi line fieldgroup
\begin{boxedminipage}[t]{\linewidth}
\begin{intens}
DESCRIPTION "Short Circuit Current Fit";

DATAPOOL
  STRUCT FitParameters {
    REAL {EDITABLE, SCALAR} Io, T, f1, tmax;
    INTEGER {EDITABLE, SCALAR} nsamples;
  };
  FitParameters {SCALAR} pars;

  STRUCT ResultStruct {
    REAL time, current;
    STRING plot;
  };
  ResultStruct results;
END DATAPOOL;

STREAMER
  input_stream{JSON}(pars);
  result_stream{JSON}(results);
END STREAMER;

OPERATOR
  PROCESS fit_plot: BATCH{"tee p.in| ./scplot.py | tee r.out"};
  PROCESSGROUP fit_plot_pg {_("Fit Plot"), HIDDEN, SILENT, NO_LOG}(
    result_stream{DISPLAY=NONE} = fit_plot(input_stream);
  );
END OPERATOR;

UI_MANAGER
  FIELDGROUP
    matplotlib_plot(results.plot:30:20 {EXPAND});

FORM main_window_form{MAIN}(
  matplotlib_plot
);

END UI_MANAGER;

END.
\end{intens}
\end{boxedminipage}
\clearpage
%\newpage
\paragraph{\MESSAGEQUEUE{} \PUBLISH{} - \SUBSCRIBE{} example with \TIMER{}}
\label{sec:opexamples:messagequeue:publishsubscribe}

The following example shows a \PUBLISH{} - \SUBSCRIBE{} \MESSAGEQUEUE{} pattern. It also
shows the usage of \TIMER{}.

\vspace{1cm}
\includegraphics[valign=t]{examples/messageQueue/publisher}
\hspace{2cm}
\includegraphics[valign=t]{examples/messageQueue/subscriber}
\vspace{1cm}

The example consists of four files:
\begin{itemize}
\item publisher.des  : INTENS application that publishes weather data using ZeroMQ.
\item subscriber.des : INTENS application that receives the published data.
\item common.inc     : Common code included in both publisher.des and subscriber.des.
\item timer.inc      : \TIMER{} code included in publisher.des.
\end{itemize}

\newpage
publisher.des:

\begin{boxedminipage}[t]{\linewidth}
\begin{intens}
DESCRIPTION "Publisher";

INCLUDE common.inc
INCLUDE timer.inc

UI_MANAGER
  FORM
    main_form { MAIN, HIDE_CYCLE } (
      weather_fg
    , timer_button_fg
    )
  ;
END UI_MANAGER;

OPERATOR
  MESSAGE_QUEUE
    weather_publisher {
      PUBLISH              // MessageQueue Pattern
    , PORT=5561            // TCP port
    }
  ;
END OPERATOR;

FUNCTIONS
  FUNC
    publish_weather_func {
      PUBLISH (
        MESSAGE_QUEUE = weather_publisher
      , HEADER="Weather"
      , RESPONSE=weather_stream
      );
    }
  ;

  FUNC
    INIT {
      weather.stationName = "Basel";
      t = 0;
    }
  ;
END FUNCTIONS;

END.
\end{intens}
\end{boxedminipage}


\newpage
subscriber.des:

\begin{boxedminipage}[t]{\linewidth}
\begin{intens}
DESCRIPTION "Subscriber";

INCLUDE common.inc

UI_MANAGER
  FORM
    main_form { MAIN, HIDE_CYCLE } (
      weather_fg
    )
  ;
END UIMANAGER;

OPERATOR
  MESSAGE_QUEUE
    weather_subscriber {
      SUBSCRIBE            // *MessageQueue Pattern
    , HOST="localhost"     // Hostname
    , PORT=5561            // TCP port
    , (
        HEADER="Weather"
      , STREAM=weather_stream
      )
    }
  ;
END OPERATOR;

END.
\end{intens}
\end{boxedminipage}

\newpage
common.inc:

\begin{boxedminipage}[t]{\linewidth}
\begin{intens}
// Common parts of the message queue examples
DATAPOOL
  STRUCT
    Weather {
      STRING
        stationName { LABEL="Station name", LABEL }
      , timestamp   { LABEL="Timestamp", STRINGTIME }
      ;
      REAL
        temperature { LABEL="Temperature", UNIT="[Â°C]" }
      , airPressure { LABEL="Air pressure", UNIT="[hPa]" }
      ;
    }
  ;
  Weather weather;
END DATAPOOL;

UI_MANAGER
  FIELDGROUP
    weather_fg (
      LABEL(weather.stationName) weather.stationName { COLSPAN=2 }
    , LABEL(weather.timestamp)   weather.timestamp   { COLSPAN=2 }
    , LABEL(weather.temperature) weather.temperature:6:1
        UNIT(weather.temperature)
    , LABEL(weather.airPressure) weather.airPressure*1e-2:6:1
        UNIT(weather.airPressure)
    )
  ;

  // Hide STDWINDOW and LOGWINDOW from MAIN form
  FORM
    stdwin_form { "Standard output", HIDECYCLE
                , CLOSE_BUTTON=NONE } (
      STD_WINDOW { SIZE=24*80 }
    )
  , logwin_form { "Log output", HIDECYCLE
                , CLOSE_BUTTON=NONE } (
      LOG_WINDOW { SIZE=24*80 }
    )
  ;
END UI_MANAGER;

STREAMER
  weather_stream { JSON } (
    weather
  );
END STREAMER;
\end{intens}
\end{boxedminipage}

\newpage
timer.inc:

\begin{boxedminipage}[t]{\linewidth}
\begin{intens}
// Timer parts for publisher.des
DATAPOOL
  REAL t;
  STRING { EDITABLE }
    start { LABEL="Start", BUTTON, FUNC=start_timer_func }
  , stop  { LABEL="Stop" , BUTTON, FUNC=stop_timer_func };
END DATAPOOL;

UI_MANAGER
  FIELDGROUP
    timer_button_fg (
      start   stop );
END UI_MANAGER;

OPERATOR
  TIMER
    publish_weather_timer {
      FUNC=update_and_publish_weather_func
    };
END OPERATOR;

FUNCTIONS
  FUNC
    update_and_publish_weather_func {
      // update (simulate) weather data
      t += 0.1;
      weather.timestamp   = CURRENT_TIME;
      weather.temperature = 15 + SIN(t) * 10;
      weather.airPressure = 102300 + COS(t) * 3000;

      RUN ( publish_weather_func );
    },
    start_timer_func {
      START (
        publish_weather_timer
      , PERIOD=1
      , DELAY=5
      );
    },
    stop_timer_func {
      STOP ( publish_weather_timer );
    };
END FUNCTIONS;
\end{intens}
\end{boxedminipage}
